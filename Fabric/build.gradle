plugins {
    id 'fabric-loom' version '0.8-SNAPSHOT'
    id 'maven-publish'
}
repositories {
    mavenCentral()
    maven { url "https://maven.shedaniel.me/" } //cloth-config
    maven {
        url "https://bai.jfrog.io/artifactory/maven" //ModMenu
        content {
            includeGroup "com.terraformersmc"
        }
    }
    maven {
        url = "https://server.bbkr.space/artifactory/libs-release" //CottonMC (LibGui)
    }
    maven { url "https://maven.blamejared.com" } //Patchouli
    maven { url "https://repo1.maven.org/maven2/" }
    flatDir{ dirs "lib" }
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = "RPMTW-Update-Mod-Fabric-1.17"
version = project.mod_version as Object
group = project.maven_group as Object

minecraft {
    accessWidener = file("src/main/resources/RpmtwUpdateMod.accesswidener")
}

configurations {
    shadow
    compile.extendsFrom(shadow)
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation("vazkii.patchouli:Patchouli:1.17.1-55-FABRIC-SNAPSHOT") //Patchouli

    shadow('commons-lang:commons-lang:2.6')
    shadow('net.sf.ezmorph:ezmorph:1.0.6')
    shadow('commons-collections:commons-collections:3.2.2')
    shadow('commons-beanutils:commons-beanutils:1.9.4')
    shadow('org.apache.httpcomponents:httpclient:4.5.13')
    include(modApi('io.socket:socket.io-client:2.0.1'))
    include(modApi('net.lingala.zip4j:zip4j:2.9.0'))
    include('io.socket:engine.io-client:2.0.0')
    include('org.json:json:20090211')
    include('com.squareup.okhttp3:okhttp:3.12.12')
    include('com.squareup.okio:okio:2.10.0')
    modImplementation include('customUtils:Utils:1.0.0')
 
    modImplementation include("me.shedaniel.cloth:cloth-config-fabric:5.0.34") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation include("io.github.cottonmc:LibGui:4.1.0+1.17")
    modImplementation("com.terraformersmc:modmenu:2.0.2")
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
        }
    }
    repositories {
    }
}
remapJar.doLast {
    task ->
        ant.checksum file: task.archivePath
}
